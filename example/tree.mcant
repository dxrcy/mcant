set delay 10

ant use tree;
    offset 0, -1, 0; -- Spawn just below the player
end

define $wood oak_wood
define $leaves oak_leaves

ruleset tree
    -- Find ground
    0, air, -> 0,, down;
    0,, -> 1,, up;

    -- Trunk below branches
    1, air, -> 2, $wood,;
    2, air, -> 3, $wood,;
    3, air, -> 4, $wood,;

    -- Trunk with alternating branches
     4, air, ->  5, $wood, +spawn ant use branch; facing east;  state 0; end;
     5, air, ->  6, $wood, +spawn ant use branch; facing west;  state 0; end;
     6, air, ->  7, $wood, +spawn ant use branch; facing north; state 0; end;
     7, air, ->  8, $wood, +spawn ant use branch; facing south; state 0; end;
     8, air, ->  9, $wood, +spawn ant use branch; facing east;  state 1; end;
     9, air, -> 10, $wood, +spawn ant use branch; facing west;  state 2; end;
    10, air, -> 11, $wood, +spawn ant use branch; facing north; state 2; end;
    11, air, -> 12, $wood, +spawn ant use branch; facing south; state 2; end;
    12, air, -> 13, $wood,;
    13, air, ->  -, $leaves,;
end

-- Place branch in the current direction
ruleset branch
    0,, -> 1,,;
    1,, -> 2, $wood,;
    2,, -> 3, $wood,;
    3,, -> 4, $wood,;
    4,, -> -, $leaves,;
end

